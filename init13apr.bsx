'{$STAMP BS2sx, main13apr.bsx, drive13apr.bsx, postdrive13apr.bsx}
'{$PORT COM1}

'--------------------------------------------------------------------------------------
'init13apr.bsx

'The is the general initialization stuff,
'and maybe some pit, calibration, & debug stuff. 

'This file runs once after a reset, and we never come back to this bank. 

'--------------------------------------------------------------------------------------------
'WildStang 2002 Robot Control System Software -- Lauren Halatek, Chris Ankeny, Keith Edwards. 

'Common Errors:
'-after calling a subroutine, remember to re-GET any working vars you need. 
'

'Version Notes:

' *** Problems/TODO: gyro overrides crab when turbo down
' steepen the outside quad slope so the max turn is a little higher. 
' save the gyro_straight value to EEPROM (once!) when manual override 
' is pressed (only if the value changed [by N?]). 
' add a safety so we don't train if the gyro is broken (reading 0 or 255). 

'13apr - add a fast zone to crab steering.  added gyro straightening code. 
'        disabled perspective when in manual override (the buttons are 
'        overloaded).  USED AT ORLANDO. 
'10apr - added ENABLE const for redundancy. 
'8apr - CLEANUP. added sensor 5 for gyro. free up some scratchpad ram. 
'5apr - when turbo pressed, only steer leading wheels and don't diff  
'       wheel speeds. also corrected supercrab left side amplification. 
'26mar - supercrab II now working. 135 degrees of left turning. USED AT
'       NORTHWESTERN. 
'22yip - offset crab pots from center. (we want crab to be able to turn 
'       90 degrees to the right, and as much as possible to the left.)  
'       Offset all: feedback "target"s, limits (done), and initializations. 
'21yip(silanti) - allow crab to hyperrotate to left. THIS IS THE VERSION THAT
'        WAS ACTUALLY USED FOR COMPETITION AT GREAT LAKES. 
'17mar - redundancy nearly done, but disabled. FREEZE VERSION for GREAT LAKES. 
'14mar - added 4th bank, cleaned up init/vars.  partial redundancy NOT DONE YET. 
'12mar - this is the last of the 3 bank code. should be good for michigan
'       if we don't go with redundancy.  FALLBACK VERSION (w/known latent 
'       defects) for GREAT LAKES.
'9mar - made dual rate quad steering. 
'8mar - fixed acceleration & reliable init. [some latent shared memory bugs]
'7mar - added debug switch on robot, to make box LEDs show pot centering. added aux wheel sync. 
'5mar - mirrored aux drive and unmirrored spin. 
'28feb - add LEDs for joystick trim zero. 
'21feb - cleaned up serin stream. 
'init16feb.bsx - adding new tank button. throttle deadzone. 
'CAUTION: both 02 bots have been re-wired.  DO NOT USE SW versions 
'prior to 2/13/02 without checking the mappings. 

'proto10feb.bsx - hook mapped to box: p3_sw_aux2/1 (out/in), tank lift temporarially on accum. 
'proto9feb_2.bsx - trigger for zeropoint, otherwise use tank spin. 
' scaling down back wheels, turning off accel until it's finished for 4 wheels 
'proto9feb.bsx (copy of proto6feb_2 with p1&p3 remapped to p2&p4) 
'proto6feb.bsx - pretty good but something a little freaky 
' w/zero point crossings in mirror mode.
'proto4feb.bsx - added hook & tank lift (don't know dirs or relayA or B)
'proto3feb.bsx  added pot 0 check & 254 & dual twitch
' added aux drives on pwms right is 6, left is 7
'proto31jan_2.bsx  acceleration code working
'proto31jan_1.bsx  hooray! first full driving 4wheel version. 
'[don't forget to disable the cranial harpoon] 
'add sensor2 & front control for crab & limits (ok for manual)
' proto30jan.bsx (based off debbie_crab30jan.bsx) + sensor2 
' check for duplicate labels
'in version 12_2.bsx, quad, crab work & tankspin. zero & accel are disabled
'(something wrong). add buttons to this & debug the rest later. 


'k: try to optimize feedback loop for better performance... 

'Consider:
'when tank is down, only allow 90 degree crab (fast)
'tank on right joystick (??)
'light off low voltage


'========= DEFINE CONSTANTS FOR INITIALIZATION ===============================
'=============================================================================
'  The initialization code is used to select the input data used by PBASIC.
'  The Master micro-processor (uP) sends the data you select to the BS2SX
'  PBASIC uP.  You may select up to 26 constants, corresponding
'  to 26 variables, from the 32 available to you.  Make sure that you have 
'  variables for all the bytes recieved in the serin command.
'
'  The constants below have a "c_" prefix, as compared to the variables that 
'  they will represent.
'
'  Set the Constants below to 1 for each data byte you want to recieve.
'  Set the Constants below to 0 for the unneeded data bytes.


'---------- Set the Initialization constants you want to read ----------------

c_p1_y		CON	0
c_p2_y		CON	1
c_p3_y		CON	0
c_p4_y		CON	1

c_p1_x		CON	1
c_p2_x		CON	1
c_p3_x		CON	1
c_p4_x		CON	1

c_p1_wheel	CON	1
c_p2_wheel	CON	0
c_p3_wheel	CON	0
c_p4_wheel	CON	0

c_p1_aux	CON	1
c_p2_aux	CON	0
c_p3_aux	CON	0
c_p4_aux	CON	0

c_oi_swA	CON	1
c_oi_swB	CON	1

c_sensor1	CON	1
c_sensor2	CON	1
c_sensor3	CON	1
c_sensor4	CON	1
c_sensor5	CON	1
c_sensor6	CON	0
c_sensor7	CON	0
c_batt_volt	CON	1

c_rc_swA	CON	1
c_rc_swB	CON	0

c_delta_t	CON	0
c_PB_mode	CON	1
c_packet_num	CON	0
c_res01		CON	0


'=============================================================================
'SHARED Constants (these are used by more than one bank and MUST AGREE in each) 

TRUE		CON 1
FALSE		CON 0


'=============================================================================
'Constants for Scratchpad ram locations [0-62] (63 reserved)
'(these constants are shared and must agree in all banks) 

'(I just reserved these in the order they come in the serin stream)

s_oi_swA	CON	0	
s_oi_swB	CON	1
s_rc_swA	CON	2
's_rc_swB	CON	3
s_p2_x		CON	4
s_p1_x		CON	5	'stored' value of p1_x
s_p4_x		CON	6
s_p3_x		CON	7
s_PB_mode	CON	8
's_packet_num 	CON	9
s_sensor1	CON	10	
s_sensor2	CON	11	
s_p2_y		CON	12	
's_p1_y		CON	13	'
s_sensor3	CON	14	
s_sensor4	CON	15	
s_p4_y		CON	16	
's_p3_y		CON	17	'
s_sensor5	CON	18	
's_sensor6	CON	19	'
's_p2_wheel	CON	20	'
s_p1_wheel	CON	21	
's_sensor7	CON	22	'
s_sensor8	CON	23	'bat_volt
's_p4_wheel	CON	24	'
's_p3_wheel	CON	25	'
's_p2_aux	CON	26	'
s_p1_aux	CON	27	
's_p4_aux	CON	28	'
's_p3_aux	CON	29	'
's_delta_t	CON	30	'
's_res01	CON	31	'
'(...end of possible serin inputs.  continue now, with outputs and 
' stored user variables.) 
'
' ...insert any other storage names up to 62 that are useful... 

s_gyro_trend	CON	32

s_gyro_straight	CON	33  'wheel target that gyro thinks is straight 
s_magictongue	CON	34

s_loopcount 	CON	35 
s_pot_bits  	CON	36   'bit field of pot health 

's_tank		CON	37   'obsolete
s_conveyor	CON	38
s_shooter	CON	39

s_relayA	CON	40
s_relayB	CON	41

s_aux_left	CON	42
s_aux_right	CON	43

s_crab_broken	CON	44  'kmk make bit pot_broken. disable proportional steering pwms if 1
s_front_crab	CON	45 
s_back_crab	CON	46
s_fcrab_target	CON	47
s_bcrab_target	CON	48

s_lf_cur	CON	49
s_rf_cur	CON	50
s_lb_cur	CON	51
s_rb_cur	CON	52

s_lf_wheel	CON	53	'stored' value for left_wheel 
s_rf_wheel	CON	54
s_lb_wheel	CON	55
s_rb_wheel	CON	56

s_bat_volt	CON	57

s_initialpot1	CON	58
s_initialpot2	CON	59
s_initialpot3	CON	60 'we COULD make these temporary to free up some memory..
s_initialpot4	CON	61

s_manybits	CON	62   'twitchdone/perspective/...

s_banknumber	CON	63  'Read-Only

' count backwards from 62.. the last address. 



'---------- Initialization Constant VOLTAGE - USER DEFINED -------------------
'  This is the 'Low Battery' detect voltage.  The 'Low Battery' LED will
'  blink when the voltage drops below this value.
'  Basically set VOLTAGE = INT ((DESIRED FLASH VOLTAGE + 0.4) * 16.3)
'  Example, for a 10 Volt trigger, set Voltage eq 170.

dataInitVolt	CON 153	'9.0 Volts


'========== DEFINE CONSTANTS (DO NOT CHANGE) =================================
'=============================================================================
' Baud rate for communications with User CPU
'OUTBAUD	CON	20	' (62500, 8N1, Noninverted)
'INBAUD		CON	20	' (62500, 8N1, Noninverted)
'
'USERCPU	CON	4
'FPIN		CON	1
COMA		CON	1
COMB		CON	2
COMC		CON	3

'--------------------------------------------------------------------------------------
'NOTE: init doesn't really need these defined for any purpose
'other than to initialize them by name..... 

tmp1		VAR byte
pwm11		VAR tmp1

tmp2		VAR byte
pwm9		VAR tmp2

p1_x		VAR byte	'Port 1, X-axis on Joystick
pwm1		VAR p1_x
tmp3		VAR p1_x

p2_x		VAR byte	'Port 2, X-axis on Joystick
pwm2		VAR p2_x
tmp4		VAR p2_x

p3_x		VAR byte	'Port 3, X-axis on Joystick
'pwm16		VAR p3_x
tmp15		VAR p3_x

p4_x		VAR byte	'Port 4, X-axis on Joystick
pwm4		VAR p4_x
tmp6		VAR p4_x

'p1_y		VAR byte	'Port 1, Y-axis on Joystick
pwm5		VAR byte
tmp7		VAR pwm5

p2_y		VAR byte	'Port 2, Y-axis on Joystick
pwm6		VAR p2_y
tmp8		VAR p2_y

'p3_y		VAR byte	'Port 3, Y-axis on Joystick
pwm7		VAR byte
'tmp9		VAR p3_y

p4_y		VAR byte	'Port 4, Y-axis on Joystick
pwm8		VAR p4_y
'tmp10		VAR p4_y

p1_wheel	VAR byte	'Port 1, Wheel on Joystick
pwm3		VAR p1_wheel
tmp11		VAR p1_wheel

'p2_wheel	VAR byte	'Port 2, Wheel on Joystick
'pwm9		VAR byte
'tmp12		VAR p2_wheel

'p3_wheel	VAR byte	'Port 3, Wheel on Joystick
'pwm12		VAR p3_wheel
'tmp13		VAR p3_wheel

'p4_wheel	VAR byte	'Port 4, Wheel on Joystick
'pwm11		VAR byte
'tmp14		VAR p4_wheel

p1_aux		VAR byte	'Port 1, Aux on Joystick
pwm10		VAR p1_aux
tmp5		VAR p1_aux

'p2_aux		VAR byte	'Port 2, Aux on Joystick
'pwm14		VAR p2_aux
'tmp16		VAR p2_aux

'p3_aux		VAR byte	'Port 3, Aux on Joystick
'pwm15		VAR p3_aux
'tmp17		VAR p3_aux

'p4_aux		VAR byte	'Port 4, Aux on Joystick
'pwm13		VAR p4_aux
'tmp18		VAR p4_aux


'---------- Robot Controller (RC) - Analog Inputs ----------------------------

sensor1		VAR byte	'RC Analog Input 1, connector pin 2
sensor2		VAR byte	'RC Analog Input 2, connector pin 16
sensor3		VAR byte	'RC Analog Input 3, connector pin 5
sensor4		VAR byte	'RC Analog Input 4, connector pin 19
sensor5		VAR byte	'RC Analog Input 5, connector pin 8
'sensor6	VAR byte	'RC Analog Input 6, connector pin 22
'sensor7	VAR byte	'RC Analog Input 7, connector pin 11
bat_volt	VAR byte	'RC Analog Input 8, hardwired to the Battery
				'Vin = ((4.7/14.7)* Battery voltage)-0.4
				'Binary Battery Voltage = (Vin/5.0 V)*255

'---------- Operator Interface - Digital Inputs ------------------------------

oi_swA		VAR byte	'OI Digital Switch Inputs 1 thru 8
oi_swB		VAR byte	'OI Digital Switch Inputs 9 thru 16


'---------- Robot Controller - Digital Inputs --------------------------------

rc_swA		VAR byte	'RC Digital Inputs 1 thru 8
'rc_swB		VAR byte	'RC Digital Inputs 9 thru 16


'---------- Robot Controller - Digital Outputs -------------------------------

relayA		VAR byte
relayB		VAR byte


'---------- Misc. ------------------------------------------------------------

PB_mode		VAR byte
'packet_num	VAR byte
'delta_t	VAR byte


pot_bits    	VAR byte
LBpot       	VAR pot_bits.bit0  '1=ok, 0=broken 
RBpot       	VAR pot_bits.bit1
LFpot       	VAR pot_bits.bit2
RFpot       	VAR pot_bits.bit3
Fpot_choice 	VAR pot_bits.bit4  '1=left, 0=right
Bpot_choice 	VAR pot_bits.bit5
Frange_bad  	VAR pot_bits.bit6  '0=pots disagree, 1=ok. 
Brange_bad  	VAR pot_bits.bit7


manybits     	VAR byte 
twitchdone   	VAR manybits.bit0
perspective   	VAR manybits.bit1
ovridetouched 	VAR manybits.bit2 
going_straight	VAR manybits.bit3  '1=trying to drive straight, 0=whatever
first_loop     	VAR manybits.bit4  'TRUE = 1st pass, FALSE = forever after


'--------------------------------------------------------------------------------------
'bank_number VAR byte

GET s_banknumber, tmp1

'kmk why does this print twice (on reset). (once on download). 
'& later it didn't print at all...

'kmk FIGURE OUT! programs run when this debug is in here, but
'I get a "basic (init) error" without it and PC can’t detect
'stamp until editor is restarted. 

debug "now running in bank ", DEC tmp1, CR 
'pulling the tether seems to allow it to pass through when
'hung in some cases. 

'--------------------------------------------------------------------------------------

Gosub SysInit

Gosub MemInit

Run 1  'no return.. just jump to bank 1. 


'=============================================================================
SysInit:

'---------- Input & Output Declarations --------------------------------------

Output		COMB
Input		COMA
Input		COMC

Output		7	'define Basic Run LED on RC => out7

Output		8	'define Robot Feedback LED => out8	=> PWM1 Green
Output		9	'define Robot Feedback LED => out9	=> PWM1 Red
Output		10	'define Robot Feedback LED => out10	=> PWM2 Green
Output		11	'define Robot Feedback LED => out11 => PWM2 Red
Output		12	'define Robot Feedback LED => out12 => Relay1 Red
Output		13	'define Robot Feedback LED => out13 => Relay1 Green
Output		14	'define Robot Feedback LED => out14 => Relay2 Red
Output		15	'define Robot Feedback LED => out15 => Relay2 Green


'---------- Initialize Inputs & Outputs --------------------------------------

Out7  = 1		'Basic Run LED on RC
Out8  = 0		'PWM1 LED - Green
Out9  = 0		'PWM1 LED - Red
Out10 = 0		'PWM2 LED - Green
Out11 = 0		'PWM2 LED - Red
Out12 = 0		'Relay1 LED - Red
Out13 = 0		'Relay1 LED - Green
Out14 = 0		'Relay2 LED - Red
Out15 = 0		'Relay2 LED - Green


'========== PBASIC - MASTER uP INITIALIZATION ROUTINE ========================
'=============================================================================
'  DO NOT CHANGE THIS!  DO NOT MOVE THIS!
'  The init routine sends 5 bytes to the Master uP, defining which data bytes to receive.
'  1)  Collect init.
'  2)  Lower the COMA line, which is the clk line for the shift out command.
'  3)  Lower COMB line to tell pic that we are ready to send init data.
'  4)  Wait for pic to lower the COMC line, signaling pic is ready for data.
'  5)  Now send out init dat to pic, all 5 bytes.
'  6)  Now set direction and levels for the COMA and COMB pins.

tempA		CON	c_p3_x <<1 + c_p4_x <<1 + c_p1_x <<1 + c_p2_x <<1 + c_rc_swB
dataInitA	CON	tempA <<1 + c_rc_swA <<1 + c_oi_swB <<1 + c_oi_swA
tempB		CON	c_sensor4 <<1 + c_sensor3 <<1 + c_p1_y <<1 + c_p2_y <<1 + c_sensor2
dataInitB	CON	tempB <<1 + c_sensor1 <<1 + c_packet_num <<1 + c_PB_mode
tempC		CON	c_batt_volt <<1 + c_sensor7 <<1 + c_p1_wheel <<1 + c_p2_wheel <<1 + c_sensor6
dataInitC	CON	tempC <<1 + c_sensor5 <<1 + c_p3_y <<1 + c_p4_y
tempD		CON	c_res01 <<1 + c_delta_t <<1 + c_p3_aux <<1 + c_p4_aux <<1 + c_p1_aux
dataInitD	CON	tempD <<1 + c_p2_aux  <<1 + c_p3_wheel <<1 + c_p4_wheel

Output	COMA
low		COMA
low		COMB
Wait_init:	if IN3 = 1 then Wait_init:
Shiftout	COMB,COMA,1, [dataInitA,dataInitB,dataInitC,dataInitD,dataInitVolt]
Input		COMA
high		COMB

return


'--------------------------------------------------------------------------------------
MemInit:

'debug "running MemInit", CR

'Initialize Ram Vars. 
'B1 = 127  'safe initial values. 
'B2 = 127
'B3 = 127
'B4 = 127
'B5 = 127
'B6 = 127
'B7 = 127
'B8 = 127
'...
'B25 = 0  'init flag: indicates main is being called from init. 


p1_x = 127		'Port 1, X-axis on Joystick
p2_x = 127		'Port 2, X-axis on Joystick
p3_x = 127		'Port 3, X-axis on Joystick
p4_x = 127		'Port 4, X-axis on Joystick

'p1_y = 127		'Port 1, Y-axis on Joystick
p2_y = 127		'Port 2, Y-axis on Joystick
'p3_y = 127		'Port 3, Y-axis on Joystick
p4_y = 127		'Port 4, Y-axis on Joystick

p1_wheel = 127		'Port 1, Wheel on Joystick
'p2_wheel = 127		'Port 2, Wheel on Joystick
'p3_wheel = 127		'Port 3, Wheel on Joystick
'p4_wheel = 127		'Port 4, Wheel on Joystick

p1_aux = 127		'Port 1, Aux Analog
'p2_aux = 127		'Port 2, Aux Analog
'p3_aux = 127		'Port 3, Aux Analog
'p4_aux = 127		'Port 4, Aux Analog

sensor1 = 127
sensor2 = 127
sensor3 = 127
sensor4 = 127
sensor5 = 127
bat_volt = 127

PB_mode = 0


'Initialize Scratchpad ram with default (safe) values:

'kmk - review this to make sure all s_'s are initialized (only once)... 

PUT s_oi_swA, 0
PUT s_oi_swB, 0
PUT s_rc_swA, 0
'PUT s_rc_swB, 0
PUT s_p2_x, 127
PUT s_p1_x, 127
PUT s_p4_x, 127
PUT s_p3_x, 127
PUT s_PB_mode, 0
'PUT s_packet_num, 0
PUT s_sensor1, 127
PUT s_sensor2, 127
PUT s_p2_y, 127
'PUT s_p1_y, 127
PUT s_sensor3, 127
PUT s_sensor4, 127
PUT s_p4_y, 127
'PUT s_p3_y, 127
PUT s_sensor5, 127
'PUT s_sensor6, 127
'PUT s_p2_wheel, 127
PUT s_p1_wheel, 127
'PUT s_sensor7, 127
PUT s_bat_volt, 127	'sensor8
'PUT s_p4_wheel, 127
'PUT s_p3_wheel, 127
'PUT s_p2_aux, 127
PUT s_p1_aux, 127
'PUT s_p4_aux, 127
'PUT s_p3_aux, 127
'PUT s_delta_t, 127
'PUT s_res01, 127


'continue initializing user variables...

PUT s_gyro_trend, 127

PUT s_relayA, 0
PUT s_relayB, 0
PUT s_shooter, 127
PUT s_conveyor, 127

PUT s_aux_left, 127
PUT s_aux_right, 127

PUT s_fcrab_target, 127  'kmk maybe add -offset (but that would mean copying CON from other bank) 
PUT s_bcrab_target, 127

PUT s_initialpot1, 0 
PUT s_initialpot2, 0 
PUT s_crab_broken, 0  'initialize to 0="not broken"
PUT s_front_crab, 127 
PUT s_back_crab, 127 

PUT s_lf_wheel, 127  'final wheel speed to write to motors 
PUT s_rf_wheel, 127
PUT s_lb_wheel, 127
PUT s_rb_wheel, 127
PUT s_lf_cur, 127
PUT s_rf_cur, 127
PUT s_lb_cur, 127
PUT s_rb_cur, 127

PUT s_magictongue, 127
PUT s_loopcount, 0
'PUT s_tank, 127
PUT s_bat_volt, 255

PUT s_initialpot1, 127
PUT s_initialpot2, 127
PUT s_initialpot3, 127
PUT s_initialpot4, 127

'pot_bits is the bit field of pot health 
LBpot = 1
RBpot = 1
LFpot = 1
RFpot = 1
Fpot_choice = 1  '1=left 
Bpot_choice = 1
Frange_bad = 1  '1=ok 
Brange_bad = 1
PUT s_pot_bits, pot_bits

'manybits is a bit field of miscelaneous stuff... 
twitchdone = 0  '1 means twitch test has finished. 
perspective = 0   '0 means front 
ovridetouched = 0  '0 means override has never been used (kmk is this obsolete?) 
going_straight = 0  '1=trying to drive straight, 0=whatever
first_loop = TRUE
PUT s_manybits, manybits


'etc...


return

'======================================================================================
